*** Settings ***
Documentation   Windows Post-install Verification
...             = Usage =
...             pybot -v ILO_IP:<iLO-IP> -v ILO_USERNAME:<iLO-Username> -v ILO_PASSWORD:<iLO-Password> -v SYSTEM_IP:<System-IP> -v SYSTEM_USERNAME:<System-Username> -v SYSTEM_PASSWORD:<System-Password> win_piv.txt
...             = Variables =
...             | ILO_IP            | Required | iLO IP address of the SUT |
...             | ILO_USERNAME      | Optional | iLO username of the SUT; default _Administrator_ |
...             | ILO_PASSWORD      | Optional | iLO password of the SUT; default _Compaq123_ |
...             | SYSTEM_IP         | Required | System IP address of the SUT |
...             | SYSTEM_USERNAME   | Optional | System username of the SUT; default _Administrator_ |
...             | SYSTEM_PASSWORD   | Optional | System password of the SUT; default _Compaq123_ |
...             = Prerequisites =
...             SPP should be installed on SUT using automation scripts.
...             Chrome browser must be installed and chromedriver must be available in system path

Library     SppLibrary
Resource    SppLibrary/resource/WinLibrary.robot
Resource    SppLibrary/resource/spp_utility.robot
Variables   SppLibrary/config/variables.py


*** Variables ***
${SUT_LOG_LOCATION}     localhost
${PIV_URL}              https://{{SYSTEM_IP}}:2381


*** Test Cases ***
Windows PIV
    [Documentation]     Post-install verification for Windows
    [Tags]              PIV

    Initialze Test Case Variables

    Ilo Connect             ${ILO_IP}       ${ILO_USERNAME}         ${ILO_PASSWORD}
    Open Connection WMI     ${SYSTEM_IP}    ${SYSTEM_USERNAME}      ${SYSTEM_PASSWORD}

    Check HPSUM Version
    Enable AutoLogOn
    Reboot Server

    PIV SPP Utility Capture Screenshot
    PIV Device Manager Info
    PIV Get Event Logs
    PIV Hpsum Log File Check
    Run Keyword And Continue On Failure     PIV Check Services
    PIV HPSUM Log Parsing
    PIV Uninstall Programs

    [Teardown]      Run Keywords
    ...             Run Keyword And Ignore Error    SppLibrary.Remove Dir   ${SUT_TEMP_DIR_WINDOWS}     AND
    ...             Run Keyword And Ignore Error    SppLibrary.SMH PIV Wrapup


*** Keywords ***
Check HPSUM Version
    [Documentation]     Author: Sriram Chowdary
    ...
    ...                 Check hpsum_log.txt file and return existence status.
    ...
    ...                 Import config file based on hpsum_log.txt file existence status.
    ...
    ...                 Example:
    ...
    ...                 | Check HPSUM Version |
    ${STATUS}           Run Keyword And Return Status               SppLibrary.File Should Exist
    ...                 C:\\Users\\Administrator\\AppData\\Local\\Temp\\temp\\hpsum_log.txt
    Run Keyword If      ${STATUS} == False      Import Variables    SppLibrary/config/hpsum_config_8.py
    ...                 ELSE IF                 ${STATUS} == True   Import Variables
    ...                 SppLibrary/config/hpsum_config_7.py
    ...                 ELSE                    Should Be True      ${HPSUM_VERSION} == 8 or ${HPSUM_VERSION} == 7
    ...                 " Didn't find HPSUM version "

